dnl $Id: configure.in,v 1.29 2023/01/30 01:08:03 tom Exp $
dnl configure-script template for "misc_tools"

AC_INIT(src/newpath/newpath.c)
CF_CHECK_CACHE
AC_CONFIG_HEADER(config.h:config_h.in)

CF_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL

AC_SET_MAKE
CF_MAKEFLAGS

CF_PROG_EXT
CF_PROG_LINT
CF_WITHOUT_X
CF_XOPEN_SOURCE

AC_PROG_EGREP
AC_PROG_FGREP

CF_DISABLE_ECHO
CF_ENABLE_WARNINGS

dnl Provide a package name, used for determining the directory where the list
dnl of setuid programs resides.  The list is needed because packaging systems
dnl such as rpm and dpkg lack a reliable way to determine an arbitrary list
dnl of setuid programs which are installed.
AC_MSG_CHECKING(for the package name to use when installing)
AC_ARG_WITH(package,
	[  --with-package=XXX      override package name (default: misc_tools)],
	[PACKAGE="$withval"],
	[PACKAGE="misc_tools"])
case x$PACKAGE in
x|xyes|xno)
	PACKAGE=misc_tools
	;;
esac
AC_MSG_RESULT($PACKAGE)
AC_SUBST(PACKAGE)

dnl This package installs a setuid/setgid program like "su", but without a
dnl password.  If sudo is present, I can use that for root (but not for the
dnl other users, because sudo alters the environment).  Filter "root" out of
dnl the hacks, into the "usual" variable in this case.
define([CF_TRIM_LIST],[
	$1=`echo "[$]$1" | sed -e 's/,/ /g' -e 's/  / /g' -e 's/^ //' -e 's/ $//'`
])
AC_PATH_PROG(SUDO_PATH, sudo, none)
AC_MSG_CHECKING(if you want to install sudo-hacks)
AC_ARG_WITH(sudo-hacks,
	[  --with-sudo-hacks=XXX   set list of id's for sudo hacks],
	[SUDO_HACKS="$withval"],
	[SUDO_HACKS="no"])
SUDO_USUAL=
if test "x$SUDO_HACKS" != xno
then
	test -z "$SUDO_HACKS" && SUDO_HACKS=root
	test "$SUDO_HACKS" = yes && SUDO_HACKS=root
	CF_TRIM_LIST(SUDO_HACKS)
	if test "x$SUDO_PATH" != xnone
	then
		SUDO_USUAL=`echo " $SUDO_HACKS " | sed -e 's/ root //'`
		if test "$SUDO_USUAL" != "$SUDO_HACKS"
		then
			SUDO_HACKS="$SUDO_USUAL"
			SUDO_USUAL=root
		else
			SUDO_USUAL=
		fi
	fi
fi
CF_TRIM_LIST(SUDO_HACKS)
CF_TRIM_LIST(SUDO_USUAL)
AC_MSG_RESULT($SUDO_HACKS)
AC_MSG_CHECKING(if you will use sudo wrapper)
AC_MSG_RESULT($SUDO_USUAL)
test "$SUDO_HACKS" = no && SUDO_HACKS=

AC_SUBST(SUDO_HACKS)
AC_SUBST(SUDO_USUAL)

AC_MSG_CHECKING(if you want all programs)
AC_ARG_WITH(everything,
	[  --with-everything       produce all programs ignoring conflicts],
	[with_everything="$withval"],
	[with_everything="no"])
AC_MSG_RESULT($with_everything)

AC_CHECK_DECL(exit)
CF_GETOPT_HEADER

AC_HAVE_HEADERS(sys/param.h)

AC_HAVE_FUNCS( \
getopt \
getline \
realpath \
)

AC_OUTPUT(makefile,[
echo "** updating makefile targets"
for name in [$]srcdir/src/*/makefile.in
do
	cf_FILE=`echo "$name" | sed -e "s,$srcdir/,," -e 's,\\.in$,,'`
	cf_DIR=`echo "$cf_FILE" | sed -e 's,/makefile,,'`
	if test -s $cf_FILE
	then
		echo "	cd $cf_DIR && \$(MAKE) \$(MAKE_RECUR) \[$]@" >>makefile
	fi
done
cat >>makefile <<CF_EOF
distclean ::
	rm -f makefile config.h config.status config.log config.cache
CF_EOF
for name in [$]srcdir/doc/makefile.in [$]srcdir/src/*/makefile.in
do
	cf_FILE=`echo "$name" | sed -e "s,$srcdir/,," -e 's,\\.in$,,'`
	cf_DIR=`echo "$cf_FILE" | sed -e 's,/makefile,,'`
	if test -s $cf_FILE
	then
		echo "	-@\$(SHELL) -c \"if test ! -f $cf_FILE ; then rmdir $cf_DIR 2>/dev/null && echo ..removed $cf_DIR ; fi; exit 0\"" >>makefile
	fi
done
echo "	-@\$(SHELL) -c \"rmdir src 2>/dev/null && echo ..removed src; exit 0\"" >>makefile
],[
echo "** checking for conflicts"
for name in makefile.in [$]srcdir/doc/*.in [$]srcdir/src/*/makefile.in
do
	cf_FILE=\`echo "\$name" | sed -e "s,\$srcdir/,," -e 's,\\.in\$,,'\`
	cf_DIR=\`echo "\$cf_FILE" | sed -e 's,/makefile,,'\`
	cf_TMP=\`basename \$cf_DIR\`
	if test $with_everything = no
	then
		case \$cf_TMP in #(vi
		perror) #(vi
			if \$cf_TMP --version 2>/dev/null >/dev/null
			then
				echo "...skipping \$cf_TMP"
				continue
			fi
			;;
		realpath)
			if \$cf_TMP --version 2>/dev/null >/dev/null
			then
				echo "...skipping \$cf_TMP"
				if test -s \$cf_FILE
				then
					echo "?? oops \$cf_FILE"
					rm -f \$cf_FILE
				fi
				continue
			else
				cf_realpath=\`realpath --version 2>/dev/null\`
				if test "x$cf_realpath" = "x`pwd`/--version"
				then
					echo "...skipping \$cf_TMP"
					continue
				fi
			fi
			;;
		esac
	fi
	CONFIG_FILES="\$CONFIG_FILES \$cf_FILE"
done
],[cat])
